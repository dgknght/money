app = angular.module 'register', ['ui.bootstrap', 'transactions']

app.controller 'AccountRegisterController', ['$scope', '$http', '$uibModal', ($scope, $http, $uibModal) ->
  _self = this
  $scope.transactionItems = []
  $scope.loadedAllTransactionItems = false
  $scope.lastPage = 0
  @accountId = null
  @accountName = null
  @new = ->
    $scope.formTransaction = {
      transaction_date: new Date(new Date().toDateString()),
      items: [
        {account_id: _self.accountId},
        {}
      ]
    }
    _self.openModal()
  @delete = (item) ->
    $http.delete("/transactions/#{item.transaction_id}.json").then (response) ->
      _self.loadTransactionItems()
    , (error) ->
      console.log "Unable to remove the transaction."
      console.log error
  @edit = (item) ->
    $http.get("/transactions/#{item.transaction_id}.json").then (response) ->
      $scope.formTransaction = response.data
      _self.openModal()
    , (error) ->
      console.log "Unable to get the transaction from the server."
      console.log error
  @openModal = ->
    $uibModal.open({
      animation: true,
      templateUrl: '<%= asset_path('transaction-form.html') %>',
      controller: 'TransactionFormController',
      size: 'lg',
      scope: $scope
    }).result.then ->
      _self.loadTransactionItems()
  $scope.transactionItemsUrl = ->
    nextPage = ($scope.lastPage || 0) + 1
    result = "/accounts/#{_self.accountId}/transaction_items.json?per_page=15&page=#{nextPage}"
    console.log "url=#{result}"
    result
  @loadTransactionItems = ->
    $http.get($scope.transactionItemsUrl()).then( (response) ->
      $scope.transactionItems = response.data
      $scope.lastPage = 1
    , (error) ->
      console.log "Unable to get the transaction items from the service"
      console.log error
    )
  $scope.loadMoreTransactionItems = ->
    return if $scope.loadingTransactionItems || $scope.allTransactionItemsLoaded
    $scope.loadingTransactionItems = true
    $http.get($scope.transactionItemsUrl()).then (response) ->
      $scope.transactionItems = $scope.transactionItems.concat(response.data)
      $scope.lastPage++
      $scope.allTransactionItemsLoaded = response.data.length == 0
      $scope.loadingTransactionItems = false
      console.log "loaded page #{$scope.lastPage} with #{response.data.length} item(s)"
  $scope.$watch 'selectedSearchHandler', (handler) ->
    $scope.allTransactionItemsLoaded = false
    $scope.lastPage = 0
    if handler && handler.key == 'account-register'
      _self.accountId = handler.accountId
      _self.accountName = handler.accountName
      _self.loadTransactionItems()
    else
      $scope.transactionItems = []
      _self.accountId = null
      _self.accountName = null

  return
]

app.directive 'accountRegister', ->
  {
    restrict: 'E',
    templateUrl: '<%= asset_path('account-register.html') %>'
  }
