app = angular.module 'transactions', []

app.controller 'TransactionFormController', ['$scope', '$uibModalInstance', '$http', ($scope, $uibModalInstance, $http) ->
  _self = this
  $scope.transactionTotals = {debits: 0, credits: 0}
  @_prepareData = ->
    transaction = $scope.formTransaction
    transaction.items_attributes = transaction.items
    transaction.items = null
    { transaction: transaction }
  @create = ->
    $http.post("/entities/#{$scope.currentEntityId}/transactions.json", _self._prepareData()).then null, (error) ->
      console.log "Unable to create the transaction."
      console.log error
  @update = ->
    $http.put("/transactions/#{$scope.formTransaction.id}.json", _self._prepareData()).then null, (error) ->
      console.log "Unable to update the transaction."
      console.log error
  $scope.save = ->
    if $scope.formTransaction.id
      _self.update().then (_) ->
        $uibModalInstance.close()
    else
      _self.create().then (_) ->
        $uibModalInstance.close()
  $scope.cancel = ->
    $uibModalInstance.dismiss('cancel')

  # Recalculate total debits and total credits
  $scope.$watchCollection ->
    _.map(['debit', 'credit'], (a) -> _self._totalByAction(a))
  , (totals) ->
    $scope.transactionTotals.debits = totals[0]
    $scope.transactionTotals.credits = totals[1]
    $scope.transactionTotals.difference = totals[0] - totals[1]

  @_totalByAction = (action) ->
    _.chain($scope.formTransaction.items).filter( (i) ->
      i.action == action
    ).reduce( (sum, i) ->
      sum + i.amount
    , 0).value()

  # Ensure there is always an empty line item at the bottom of the list
  $scope.$watch ->
    return true unless $scope.formTransaction
    last = _.last($scope.formTransaction.items)
    if last then _.chain(last).pick('amount', 'account_id', 'action').isEmpty().value() else false
  , (isEmpty) ->
    return unless $scope.formTransaction && $scope.formTransaction.items
    $scope.formTransaction.items.push({}) unless isEmpty
]

app.controller('PurchasesController', ['$scope', '$http', ($scope, $http) ->
  _self = this
  @newPurchase = { transactionDate: today() }
  @enteredPurchases = []
  @categoryAccounts = ->
    _.filter($scope.accounts, (a) -> a.account_type == 'expense')
  @methodAccounts = ->
    _.filter($scope.accounts, (a) -> _.contains(['liability', 'asset'], a.account_type)) # TODO Use tags to select appropriate accounts
  @_prepareData = ->
    {
      transaction:
        {
          transaction_date: @newPurchase.transactionDate,
          description: @newPurchase.location,
          items_attributes:
            [
              {
                action: 'credit',
                account_id: @newPurchase.method,
                amount: @newPurchase.amount
              },
              {
                action: 'debit',
                account_id: @newPurchase.category,
                amount: @newPurchase.amount
              }
            ]
        }
    }
  @save = (form) ->
    data = @_prepareData()
    $http.post("/entities/#{$scope.currentEntityId}/transactions.json", data).then (_) ->
      next = {
        transactionDate: _self.newPurchase.transactionDate,
        method: _self.newPurchase.method
      }
      _self.enteredPurchases.push _self.newPurchase

      console.log "enteredPurchases"
      console.log _self.enteredPurchases

      _self.newPurchase = next
      form.$setPristine()
      form.$setUntouched()
      return
    , (error) ->
      console.log "Unable to create the transaction."
      console.log error
  return
])

app.directive 'purchaseEntry', ->
  {
    restrict: 'E',
    templateUrl: '<%= asset_path('purchase-entry.html.haml') %>'
  }
